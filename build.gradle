buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
    }
}

allprojects {
    group = 'io.github.plastiv.barcodescanner'
    version = '1.5'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    ext {
        supportLibraryDependency = 'com.android.support:support-v4:20.0.0'
        isLibrary = false
    }
}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("android") || ext.pluginContainer.hasPlugin("android-library")) {
            android {
                compileSdkVersion 22
                buildToolsVersion '21.1.2'

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion 8
                    targetSdkVersion 19
                    versionCode 12
                    versionName "1.2"
                }
            }
        }
        if(project.isLibrary) {
            configure(project) {
                apply plugin: 'maven'
                apply plugin: 'signing'

                signing {
                    required false
                    sign configurations.archives
                }

                uploadArchives {
                    repositories.mavenDeployer {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        repository(url: "file://C:\\Users\\spechenizkyi\\Workspaces\\ZBarcodeScannerRepo")

                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/dm77/barcodescanner'

                            scm {
                                url 'scm:git@github.com:dm77/barcodescanner.git'
                                connection 'scm:git@github.com:dm77/barcodescanner.git'
                                developerConnection 'scm:git@github.com:dm77/barcodescanner.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'dm77'
                                    name 'Dushyanth Maguluru'
                                    email 'dushyanth.maguluru@gmail.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
